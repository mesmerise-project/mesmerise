buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.sf.proguard:proguard-gradle:6.2.2'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id 'application'
}

group 'se.mesmeri'
version '0.1-pre1'

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'deps'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.beust:klaxon:5.0.1'
    implementation "org.ini4j:ini4j:0.5.4"
    implementation "io.ktor:ktor-server-netty:1.3.2"
    implementation "org.slf4j:slf4j-api:1.7.30"
    implementation "ch.qos.logback:logback-classic:1.2.3"
    implementation "com.xenomachina:kotlin-argparser:2.0.7"
    implementation "com.github.Inkapplications.Shade:shade:1.2.0"
    implementation name: "jl1.0"
}
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task minimizedJar(type: proguard.gradle.ProGuardTask, dependsOn: shadowJar) {
    injars "build/libs/mesmerise-$version-all.jar"
    outjars "build/libs/mesmerise-${version}-min.jar"
    libraryjars System.properties.'java.home' + "/jmods/java.base.jmod"
    libraryjars System.properties.'java.home' + "/lib/rt.jar"
    printmapping "build/libs/mesmerise-${version}.map"
    ignorewarnings
    dontobfuscate
    dontoptimize
    dontwarn
    keeppackagenames "se.mesmeri.mesmerise"
    keepdirectories  "se/mesmeri/mesmerise"

    def keepClasses = [
            'io.ktor.server.netty.EngineMain',
            'kotlin.reflect.jvm.internal.**',
            'se.mesmeri.mesmerise.**',
            'com.beust.klaxon.DefaultConverter',
            'kotlin.text.RegexOption',
            'inkapplications.shade.1**'
    ]

    for (keepClass in keepClasses) {
        keep access: 'public', name: keepClass, {
            method access: 'public'
            method access: 'private'
        }
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'se.mesmeri.mesmerise.MesmeriseKt'
    }
}

mainClassName = 'se.mesmeri.mesmerise.MesmeriseKt'